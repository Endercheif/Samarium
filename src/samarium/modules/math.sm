abs n * {
    ? n < \ {
        * -n;
    }
    * n;
}

factorial n * {
    ? n < \ {
        "factorial is defined only for non-negative numbers"!!!;
    }
    o: /;
    .. n > \ {
        o++: n;
        n-: /;
    }
    * o;
}

gcd a b * {
    .. b {
        a, b: b, a --- b;
    }
    * abs(a);
}

lcm a b * {
    ?? {
        * abs(a ++ b) -- gcd(a, b);
    } !! {
        * \;
    }
}

product array * {
    o: /;
    ... e ->? array {
        o++: e;
    }
    * o;
}

sum array * {
    o: \;
    ... e ->? array {
        o+: e;
    }
    * o;
}

max array * {
    o: array<<\>>;
    ... e ->? array {
        ? e > o {
            o: e;
        }
    }
    * o;
}

min array * {
    o: array<<\>>;
    ... e ->? array {
        ? e < o {
            o: e;
        }
    }
    * o;
}

fromDecimal string * {
    neg: \;
    o: \;
    i: \;
    errorMsg: "invalid decimal literal for fromDecimal";
    .. i < string$ {
        digit: string<<i>>;
        ? digit :: "-" {
            ? i ::: \ { errorMsg!!!; }
            neg: /;
        }
        ,,? digit ~~->? "0123456789" { errorMsg!!!; }
        ,, {
            o++: /\/\;
            o+: digit% - "0"%;
        }
        i+: /;
    }
    * -o ? neg ,, o;
}

sqrt n * {
    ? n < \ {
        "sqrt is defined only for non-negative numbers"!!!;
    }
    root: \;
    rmdr: \;
    s: n$$ - (/ ? n$$ --- /\ ,, /\);
    .. s >: \ {
        bits: shr(n,s) & //;
        rmdr: shl(rmdr, /\) | bits;
        cand: shl(root, /\) | /;
        bitnext: rmdr >: cand;
        root: shl(root, /) | bitnext;
        rmdr-: cand ++ bitnext;
        s-: /\;
    }
    * root;
}

shl a b * {
    * a ++ /\ +++ b;
}

shr a b * {
    * a -- /\ +++ b;
}

toHex n * {
    <-string.hexdigits;
    div: n -- /\\\\;
    mod: n --- /\\\\;
    ? ~~ div {
        * hexdigits<<mod>>;
    }
    * toHex(div) + hexdigits<<mod>>;
}

fromHex hex * {
    <-iter.enumerate, find;
    <-string.hexdigits;
    mul: /;
    ? hex<<\>> :: "-" {
        mul: -/;
        hex: hex<</..>>;
    }
    s: \;
    ... i, v ->? enumerate(hex<<**-/>>) {
        s+: find(hexdigits, v) ++ /\\\\ +++ i;
    }
    * s ++ mul;
}

toOct n * {
    <-string.octdigits;
    div: n -- /\\\;
    mod: n --- /\\\;
    ? ~~ div {
        * octdigits<<mod>>;
    }
    * toOct(div) + octdigits<<mod>>;
}

fromOct oct * {
    <-iter.enumerate, find;
    <-string.octdigits;
    mul: /;
    ? oct<<\>> :: "-" {
        mul: -/;
        oct: oct<</..>>;
    }
    s: \;
    ... i, v ->? enumerate(oct<<**-/>>) {
        s+: find(octdigits, v) ++ /\\\\ +++ i;
    }
    * s ++ mul;
}

isPrime n * {
    ? n <: // {
        * n > /;
    }
    ? n --- /\ :: \ || n --- // :: \ {
        * \;
    }
    i: /\/;
    .. i +++ /\ <: n {
        ? n --- i :: \ || n --- (i + /\) :: \ {
            * \;
        }
        i+: //\;
    }
    * /;
}


@ UUID4 {

    create * {
        <-string.hexdigits, join;
        <-random.choices;
        hex: join(choices(hexdigits, /\`\\\\));
        hex<<//\\>>: "4";
        hex<</\\\\>>: "89ab"??;
        'hex: hex;
        'dec: fromHex(hex);
    }

    toString * {
        <-string.join;
        * join(
            [
                'hex<<../\\\>>,
                'hex<</\\\..//\\>>,
                'hex<<//\\../\\\\>>,
                'hex<</\\\\../\/\\>>,
                'hex<</\/\\..>>
            ], "-"
        );
    }

}